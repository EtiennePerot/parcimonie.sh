# Maintainer: Frederik Schwan <frederik dot schwan at linux dot com>
# Contributor: Etienne Perot <etienne at perot dot me>

pkgname=parcimonie-sh-git
pkgver=0
pkgrel=4
pkgdesc='Bash reimplementation of parcimonie - Refresh your GnuPG keyring without disclosing your whole contact list to the world'
arch=('any')
url='https://github.com/EtiennePerot/parcimonie.sh'
license=('custom:WTFPL')
depends=('bash' 'torsocks' 'tor' 'gnupg')
makedepends=('git')
source=('git://perot.me/parcimonie.sh'
        'pgp-key::https://perot.me/pgp-minimal.asc')
sha512sums=('SKIP'
            '563e341607135f9c1971a6be1ce526af9ac218f4a63cb532ed4b849d360104c60e013429daf5d44b4123d87608238555c7e867f7375d19f743a0cfb2bc5575e8')

pkgver() {
	cd "$startdir/parcimonie.sh"
	echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

check() {
	cd "$srcdir/parcimonie.sh"
	msg2 'Verifying GPG signature on HEAD commit.'
	export GNUPGHOME="$(pwd)/.gnupg"
	mkdir -p "$GNUPGHOME"
	chmod 700 "$GNUPGHOME"
	gpg --import < "$srcdir/pgp-key"
	gpgKeyId="$(gpg --show-key --batch --with-colons < "$srcdir/pgp-key" | grep '^sub:' | head -1 | cut -d ':' -f 5)"
	git log --max-count=1 --pretty="format:%H,%G?,%GK" HEAD | grep -q ",[GU],${gpgKeyId}\$"
	returnValue="$?"
	if [ "$returnValue" -eq 0 ]; then
		msg2 'Latest commit is properly signed.'
	else
		error "Latest commit '$(git rev-parse HEAD)' is not signed by GPG key '$gpgKeyId'."
	fi
	rm -r "$GNUPGHOME"
	unset GNUPGHOME
	return "$returnValue"
}

package() {
	cd "$srcdir/parcimonie.sh"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m644 README.md "${pkgdir}/usr/share/parcimonie.sh/README.md"
	install -D -m755 parcimonie.sh "${pkgdir}/usr/share/parcimonie.sh/parcimonie.sh"
	install -D -m644 pkg/parcimonie.sh@.service "${pkgdir}/usr/lib/systemd/system/parcimonie.sh@.service"
	install -D -m644 pkg/parcimonie.sh.user.service "${pkgdir}/usr/lib/systemd/user/parcimonie.sh.service"
	install -D -m644 pkg/sample-configuration.conf.sample "${pkgdir}/etc/parcimonie.sh.d/sample-configuration.conf.sample"
	install -D -m644 pkg/all-users.conf "${pkgdir}/etc/parcimonie.sh.d/all-users.conf"
	mkdir -p "${pkgdir}/usr/bin"
	ln -sf /usr/share/parcimonie.sh/parcimonie.sh "${pkgdir}/usr/bin/parcimonie.sh"
}
